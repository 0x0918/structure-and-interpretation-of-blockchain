## 版本标识
为了避免未来可能引入不兼容的编译器所编译，源文件需要在开头标识合约的版本，例子：
```solidity
pragma solidity ^0.5.0;
```
这样源文件既不允许低于0.5.0版本的编译器编译，也不允许高于（包含）0.6.0版本的编译器编译。

## 源文件导入
Solidity同样支持从不同的文件里导入方法，其语法非常像JavaScript的ES6标准。
全局导入
```
import "filename"
```
此语句将从 “filename” 中导入所有的全局符号到当前全局作用域中。

```
import * as symbolName from "filename";
```
创建一个新的全局符号 symbolName，其成员均来自 "filename" 中全局符号。

```
import {symbol1 as alias, symbol2} from "filename";
```
创建新的全局符号 alias 和 symbol2，分别从 "filename" 引用 symbol1 和 symbol2。

另一种语法不属于 ES6，但或许更简便：
```
import "filename" as symbolName;
```
这条语句等同于 import * as symbolName from "filename";

## 路径
上文中的 filename 总是会按路径来处理，以 / 作为目录分割符、以 . 标示当前目录、以 .. 表示父目录。 当 . 或 .. 后面跟随的字符是 / 时，它们才能被当做当前目录或父目录。 只有路径以当前目录 . 或父目录 .. 开头时，才能被视为相对路径。

用 import "./x" as x; 语句导入当前源文件同目录下的文件 x 。 如果用 import "x" as x; 代替，可能会引入不同的文件（在全局 include directory 中）。

## 声明及默认值
值类型
值类型包含
- 布尔(Booleans)
- 整型(Integer)
- 地址(Address)
- 定长字节数组(fixed byte arrays)
- 有理数和整型(Rational and Integer Literals，String literals)
- 枚举类型(Enums)
- 函数(Function Types)
为什么会叫值类型，是因为上述这些类型在传值时，总是值传递。比如在函数传参数时，或进行变量赋值时。

变量声明后均有一个初值，是对应类型的“零态”，意即对应的类型的字节表示的全0。使用中需要特别小心的是这与其它语言的默认值如null或undefined有所不同，因为有时0也是一种业务值。下面我们来看一些常见类型的声明，以及它们的默认值：
```
pragma solidity ^0.5.0;

contract DeclareOfElement{
    bool b;//fasle

    uint i;//0

    address addr;//0x0

    bytes32 by;//0x0000000000000000000000000000000000000000000000000000000000000000

    bytes varBy;//0x

    string str;//

    uint8[] arr;//

}

```

在上述例子中，bool的默认值为false，bytes32的默认值为32字节长的0。对于引用类型，bytes类型默认值为空字节数组，string为默认值为空串，动态数组uint8[] arr为空。

### 地址
Solidity还针对区块链场景引入了一些特别的类型，地址（Address）类型。地址类型表示以太坊地址的长度，大小为20个字节，160位，用一个`uint160`编码。

所有的地址都会继承地址对象，也可以随时将一个地址串，得到对应的代码进行调用。当然如果地址代表一个普通账户时，就没有那么多丰富的功能了。

### 地址字面量

十六进制的字符串，凡是能通过地址合法性检查的，都会被认为是地址，如`0xdCad3a6d3569DF655070DEd06cb7A1b2Ccd1D3AF`。

地址的`balance`方法，可以通过它得到一个地址的余额；

```
pragma solidity ^0.5.0;

contract addressTest{
    function getBalance(address addr) public view returns (uint){
        return addr.balance;
    }
}
```

如果只是想得到当前合约的余额，其实还可以用`this`改写：

```
pragma solidity ^0.5.0;

contract addressTest{
    function getBalance() public view returns (uint){
        return address(this).balance;
    }
}
```

### 地址的方法send()
用来向某个地址发送货币(货币单位是wei)。
```
pragma solidity ^0.5.0;

contract Payable{

    address payable owner;

    constructor () public {
        owner = msg.sender;
    }
    
    function GetBlance(address addr) public view returns (uint256) {
        return addr.balance;
    }

    function Transfer() public payable returns(address, uint256){
        owner.transfer(msg.value);
        return (msg.sender, msg.value);
    }
}
```
这个合约实现的是充值。this.send(msg.value)意指向合约自身发送msg.value量的以太币。msg.value是合约调用方附带的以太币。

`send`方法在发送以太币失败以后只会返回false，并不会发生一个error，社区的一些开发者觉得这样不是太安全，最后经过社区讨论在#610这个讨论中确定加入，毕竟以太坊那么值钱了，所以在solidity 0.4.13中引入了一个新的转账方法`transfer`。

transfer执行失败则会throw，这也就意味着使用send时一定要判断是否执行成功。默认情况下最好使用transfer（因为内置了执行失败的处理），二者在发送的同时传输2300gas，gas数量不可调整。

## 跨合约调用
以太坊为了避免了合约成为数据孤岛，增加了跨合约调用的支持，简言之就是一个合约可以调用另一个合约，跨合约调用大大增强了智能合约的互操作性，简化了开发成本。同时扩展了业务的想象空间。

跨合约调用必须是确定性的静态调用——在运行前即知晓被调用合约的地址，且调用结果是确定性的。

Solidity 中用`call`，`callcode`，`delegatecall`这三个函数来进行跨合约的调用。三者又有所区别。

### call vs callcode
call 和 callcode 的区别在于：代码执行的上下文环境不同。具体来说，call 修改的是被调用者的storage，而 callcode 修改的是调用者的storage。换用另外一种说法是，callcode 通过调用其他的智能合约的函数，来修改自己的智能合约的变量。而 call 通过调用其他的智能合约的函数，来修改被调用智能合约的状态。如下图所示：

![](https://github.com/Ice-Storm/structure-and-interpretation-of-blockchain/blob/master/img/chapter_4/4_1.png?raw=true)

### callcode vs delegatecall
callcode 已经被官方推荐使用 delegatecall 替代了。如果使用，编译器会提示`Warning: "callcode" has been deprecated in favour of "delegatecall"`。

callcode 与 delegatecall 都是通过调用其他的智能合约的函数，来修改自己的智能合约的变量。区别就是msg.sender不同。具体来说，delegatecall 会一直使用原始调用者的地址，而 callcode 不会。如下图所示：

![](https://github.com/Ice-Storm/structure-and-interpretation-of-blockchain/blob/master/img/chapter_4/4_2.png?raw=true)


上面的 callcode 跟 delegatecall 都是通过调用其他的智能合约的函数，来修改自己的智能合约的变量。如果自己合约中不存在这个变量，Solidity是怎么处理的呢？Solidity 会智能地新建一个变量。但实际是在一个未定义的位置存储了这个新建的变量的值，智能合约根本没法访问到。


## 引用类型的初始化
对于值类型，声明变量后，即赋值为默认值，可正常使用。而对于引用类型是否仍需同其它语言一样进行显式初始化，进行内存分配，才能进一步使用呢，引用类型相对复杂，占用空间较大的。在拷贝时占用空间也比较大。所以solidity也和其他语言一样通过引用传递。常见的引用类型有：

- 不定长字节数组（bytes）
- 字符串（string）
- 数组（Array）
- 结构体（Struts）

### 动态数组
对于数组，声明后，仍需分配内存后方可访问，下面的代码会报错Exception during execution. (invalid opcode)。

```
pragma solidity ^0.5.0;

contract initial{
  function f() public pure returns (bytes1, uint8){
    bytes memory bs;
    uint8[] memory arr;
    return (bs[0], arr[0]);
  }
}
```
由于上例中，我们越界访问元素，故抛出了异常。如果要主动分配内存，进行初始化，如何做呢，一起来看看下面的实现。

```
pragma solidity ^0.5.0;

contract initial{
  function f() public pure returns (bytes1, uint8){
    bytes memory bs = new bytes(1);
    uint8[] memory arr = new uint8[](1);
    return (bs[0], arr[0]);
  }
}

```
上述代码通过new关键进行了内存分配，现在即可正常访问第一个数组元素了。

### 映射
映射的声明后，不用显式初始化即可使用，只是里面不会有任何值，下面是一个非常简单的映射的例子。

```
pragma solidity ^0.5.0;

contract DeclareOfMapping{
  mapping(uint => string) bar;

  function f() public returns (string memory){
    bar[0] = "foo";
    return bar[0];
  }
}
```
上面的例子中，我们定义了一个映射，然后返回了映射中其中的一个元素值。由于没有存值，故这里将返回的是空串。

### 枚举
枚举类型不用显式初始化，默认值将为0。即顺位第一个值。下面来看一个枚举的示例。

```
pragma solidity ^0.5.0;

contract DeclareOfEnum{
  enum Light{RED, GREEN, YELLOW}
  Light light;
  
  function f() public pure returns (Light){
    return Light.GREEN;
  }
}

```

上面的代码的输出结果为1，RED，GREEN，YELLO的值分别为0，1，2。

### 结构体
结构体声明后，不用显式初始化即可使用。当没有显式初始化时，其成员值均为默认值。一起来看一个结构体声明的例子：

```
pragma solidity ^0.5.0;

contract DeclareOfStruct{
  struct people{
    string name;
    uint age;
  }

  people qyuan = people({
      name: "hehao",
      age: 25
  });

  function f() public view returns(string memory, uint) {
    return (qyuan.name, qyuan.age);
  }
}

```
上面的代码中定义了一个结构体，并创建了一个变量people。所有结构体内的成员值均被赋值，上述代码运行后，将返回"hehao", 25。

delete操作符
使用delete操作符会将对象重置为默认值2。

## 数据位置
复杂类型，如`数组(arrays)`和`结构体(struct)`在Solidity中有一个额外的属性，数据的存储位置，可选的为`memory`和`storage`。
memory存储位置同我们普通程序的内存一致。即分配，即使用，越过作用域即不可被访问，等待被回收。而在区块链上，由于底层实现了图灵完备，故而会有非常多的状态需要永久记录下来。比如，参与众筹的所有参与者。那么我们就要使用storage这种类型了，一旦使用这个类型，数据将永远存在。

基于程序的上下文，大多数时候这样的选择是默认的，我们可以通过指定关键字storage和memory修改它。

默认的函数参数，包括返回的参数，他们是memory。默认的局部变量是storage的1。而默认的状态变量（合约声明的公有变量）是storage。

另外还有第三个存储位置calldata。它存储的是函数参数，是只读的，不会永久存储的一个数据位置。外部函数的参数（不包括返回参数）被强制指定为calldata。效果与memory差不多。

数据位置指定非常重要，因为不同数据位置变量赋值产生的结果也不同。在memory和storage之间，以及它们和状态变量（即便从另一个状态变量）中相互赋值，总是会创建一个完全不相关的拷贝。

将一个storage的状态变量，赋值给一个storage的局部变量，是通过引用传递。所以对于局部变量的修改，同时修改关联的状态变量。但另一方面，将一个memory的引用类型赋值给另一个memory的引用，不会创建另一个拷贝。

```
pragma solidity ^0.4.0;

contract DataLocation{
  uint valueType;
  mapping(uint => uint) public refrenceType;

  function changeMemory(){
    var tmp = valueType;
    tmp = 100;
  }

  function changeStorage(){
    var tmp = refrenceType;
    tmp[1] = 100;
  }

  function getAll() returns (uint, uint){
    return (valueType, refrenceType[1]);
  }
}
```
下面来看下官方的例子说明：

pragma solidity ^0.4.0;

contract C {
    uint[] x; // the data location of x is storage

    // the data location of memoryArray is memory
    function f(uint[] memoryArray) {
        x = memoryArray; // works, copies the whole array to storage
        var y = x; // works, assigns a pointer, data location of y is storage
        y[7]; // fine, returns the 8th element
        y.length = 2; // fine, modifies x through y
        delete x; // fine, clears the array, also modifies y
        // The following does not work; it would need to create a new temporary /
        // unnamed array in storage, but storage is "statically" allocated:
        // y = memoryArray;
        // This does not work either, since it would "reset" the pointer, but there
        // is no sensible location it could point to.
        // delete y;
        g(x); // calls g, handing over a reference to x
        h(x); // calls h and creates an independent, temporary copy in memory
    }

    function g(uint[] storage storageArray) internal {}
    function h(uint[] memoryArray) {}
}
总结
强制的数据位置(Forced data location)
外部函数(External function)的参数(不包括返回参数)强制为：calldata
状态变量(State variables)强制为: storage
默认数据位置（Default data location）
函数参数（括返回参数：memory
所有其它的局部变量：storage


## 作用域
变量无论在函数内什么位置定义，其作用域均为整个函数，而非大多数据语言常见的块级作用域。下面的例子会报错`DeclarationError: Undeclared identifier.`。

```
pragma solidity ^0.5.0;

contract ScopeErr{
  function f() pure public returns (uint8) {
    { uint8 a = 0;}

    return a;
  }
}

```
上例中，由于变量作用域不同，导致a在函数里面并没有被声明，在返回的时候就会报错。

我们来看一个稍微隐蔽点的例子：
```
pragma solidity ^0.5.0;

contract FunctionScope{
  function f() public view returns (uint8){
    for(var i = 0; i < 10; i++){
      //do sth
    }
    return i;
  }
}

```
读者可以试想一下上述代码能否编译通过呢。
答案是不可以的，原因就是因为i变量虽然是在for循环中被定义，但它的的作用域不是整个函数，但是在函数内的任何位置均可以访问到这个变量。


## 合约结构
在Solidity中合约类似于面向对象编程语言中的类，每个合约中可以包含 `状态变量`、 `函数`、 `函数修饰器`、`事件`、 `结构类型`和`枚举类型`且合约可以从其他合约继承。

### 函数
函数是合约中代码的可执行单元，除了对区块链分布式系统设计的`internal`和`external`这两种不同的函数调用方式，Solidity还提供对函数可见性控制的语法。
Solidity封装了两种函数的调用方式`internal`和`external`两种。

`internal`调用，实现时转为简单的EVM跳转，可以直接使用上下文环境中的数据，对于引用传递将会变得非常高效（不用拷贝数据）。在当前的代码单元内，如对合约内函数，引入的函数库，以及父类合约中的函数直接使用即是以`internal`方式的调用；
```
pragma solidity ^0.5.0;

contract SimpleAuction {
   function f() public {}
   function callInternally() public {
       f();
   }
}
```
在上述代码中，callInternally()以internal的方式对f()函数进行了调用。

`external`调用，实现为合约的外部消息调用。合约初始化时不能`external`的方式调用自身函数，因为合约还未初始化完成。
```
pragma solidity ^0.5.0;

contract A {
    function f() public pure returns (uint8) {
        return 0;
    }
}

contract B {
    function callExternal(A a) public pure returns (uint8) {
        return a.f();
    }
}
```
虽然合约A和B的代码在一个文件中，但部署到区块链上后，它们是完全独立的两个合约，它们之间的调用方法是通过消息调用的，上述代码中，在合约B中的`callExternal()`以`external`的方式调用了合约`A`的`f()`。
`external`调用时，实际是向目标合约发送一个消息调用，消息中的函数定义部分是一个24字节大小的消息体 ，20个字节的地址，4个字节的函数签名。
`external`调用还有另一种写法，可以在合约的调用函数前加`this`来强制的以`external`方式调用，需要注意这里的`this`和大多数语言的意思都不一致。
```
pragma solidity ^0.5.0;

contract A {
    function f() public {}
    function callExternally() public {
        this.f();
    }
}
```
Solidity的函数除了`internal`和`external`的调用方式区别之外，还有函数可见性的修饰语法。

### 函数可见性
Solidity为函数提供了四种可见性，`external`， `public`， `internal`， `private`。

`external`
- 声明为`external`的函数可以从其他合约或者通过`Transaction`调用，所以声明为`external`的函数是合约对外接口的一部分。
- 不能以`internal`的方式进行调用。
- 有时在接收大的数组时性能会更好。

```
pragma solidity ^0.4.5;

contract FuntionTest{
    function externalFunc() external{}
    function callFunc(){
        //以`external`的方式调用函数
        this.externalFunc();
    }
}
```
声明为external的externalFunc()只能以external的方式进行调用，以internal的方式调用会报`Error: Undeclared identifier.`。

`public`
声明为`public`的函数既可以用`internal`的方式调用，也允许以`external`的方式调用，也是合约对外接口的一部分。
```
pragma solidity ^0.4.5;

contract FuntionTest{
    //默认是public函数
    function publicFunc(){}
    // function publicFunc(){} 等价于 function publicFunc() public {}

    function callFunc(){
        //以`internal`的方式调用函数
        publicFunc();
        
        //以`external`的方式调用函数
        this.publicFunc();
    }
}
```
我们可以看到声明为public的publicFunc()允许两种调用方式。

`internal`
在当前的合约或继承的合约中，只允许以internal的方式调用。
```
pragma solidity ^0.4.5;

contract A{
    //默认是public函数
    function internalFunc() internal{}

    function callFunc(){
        //以`internal`的方式调用函数
        internalFunc();
    }
}
contract B is A{
    //子合约中调用
    function callFunc(){
        internalFunc();
    }
}
```
上述例子中声明为internal的internalFunc()在定义合约，和子合约中均只能以internal的方式可以进行调用。

`private`
只能在当前合约中被访问（不可在被继承的合约中访问）。
即使声明为private，仍能被所有人查看到里面的数据。访问权限只是阻止了其它合约访问函数或修改数据。
```
pragma solidity ^0.4.5;

contract A{
    //默认是public函数
    function privateFunc() private{}

    function callFunc(){
        //以`internal`的方式调用函数
        privateFunc();
    }
}
contract B is A{
    //不可调用`private`
    function callFunc(){
        //privateFunc();
    }
}
```
上述例子中，声明为private的privateFunc()只能在定义的合约中以internal的方式进行调用。

### 函数装饰器
使用 修饰器 可以轻松改变函数的行为。 例如，它们可以在执行函数之前自动检查某个条件。 修饰器 是合约的可继承属性， 并可能被派生合约覆盖。

`pure`装饰器，在函数中使用pure装饰器后表示函数不会修改或访问状态变量，对外界没有任何影响。
```
pragma solidity ^0.4.23;
contract HelloWorld{
    function testPure(uint a,uint b) public pure returns(uint){
        return a+b;
    }
}
```
比如`testPure`函数，既没有修改传递进来的值（值传递），又没有修改状态变量，只是单纯的进行了一个计算，并不会消耗任何gas，应为执行使用的是本地cpu计算，并没有消耗任何链上资源。

`view`装饰器，在函数中使用view装饰器后表示函数不会修改状态变量，对链上只有读取操作，同样也不消耗gas。其实很简单，因为作为一个全节点来说，会同步保存所有的信息，保存在本地中，那么我们要查看区块链上的资源，同样可以直接在一个全节点之上查询数据即可，我不需要全世界的节点都知道。都去同时的处理这笔事务。我也不需要将调用这笔函数的信息记录在区块链上。
```
pragma solidity ^0.5.0;

contract HelloWorld{
    string public name = "qyuan";
    function getName() public view returns (string memory) {
        return name;
    }
}
```

`payable`装饰器，允许函数在调用时可以接收和发送以太币。
`constant`装饰器等同于`view`装饰器，不过在0.5.0后面的版本移除了。

### 事件
事件是能方便地调用以太坊虚拟机日志功能的接口，说到事件必然就会提到日志，事件被触发后就会被记录到区块链上就成为了日志，事件是一种行为，日志是对这种行为的记录。
事件是以太坊EVM提供的一种基础设施，用来实现一些交互功能，比如通知UI，返回函数调用结果等。
当定义的事件被触发时，被记录到日志中，日志又与合约相关联一起存储到区块链中，只要某个区块可以访问，其相关的日志就能访问，但是在合约中，并不能直接访问日志中的事件数据。
```
pragma solidity ^0.4.21;
contract SimpleAuction {
    event HighestBidIncreased(address bidder, uint amount); // 事件

    function bid() public payable {
        // ...
        emit HighestBidIncreased(msg.sender, msg.value); // 触发事件
    }
}
```
使用`event`关键字定义一个事件，参数列表中为要记录的日志参数的名称及类型。
### 监听事件
```
var event = myContract.HighestBidIncreased(function(error, result){   
    for(let key in result){
        console.log(key + " : " + result[key]);
    }
});
```
### 检索日志
`Indexed`属性，可以在事件参数上增加`Indexed`属性，最多可以对三个参数增加这样的属性，加上这个属性，就可以允许在web3.js中通过对加了这个属性的参数值进行过滤。
```
var event = myContract.transfer({value: "100"});
```
上面实现的是对value值为100的日志，过滤后的返回。
如果你想同时匹配多个值，还可以传入一个要匹配的数组。
```
var event = myContract.transfer({value: ["99","100","101"]});
```
增加了`indexed`的参数值会存到日志结构的`Topic`部分，便于快速查找。而未增加`indexed`的参数会存在`data`部分成为了原始日志。需要注意的是，如果增加`indexed`属性的是数组（包括`string`和`bytes`），那么只会在`Topic`存储对应的sha3哈希值，不会存储原始数据。
因为Topic是用于快速查找的，不能存任意长度的数据，所以通过Topic实际存在的是数组这种非固定长度数据的哈希结果，要查找时，是将要查找的内容与Topic内容进行匹配，但不能反推哈希结果，从而不能得到原始的值。

## 合约继承
继承通过关键字`is`实现，下面是一个例子；
```
pragma solidity ^0.4.0;

contract Person {
    string name;
    uint age;
}

contract Manager is Person {
}
```
上面的例子中，合约`Manager`继承了`Person`合约，继承的合约`Manager`可以访问所有的非私有成员。
```
pragma solidity ^0.4.0;

contract A{
  uint stateVar;

  function somePublicFun() public{}
  function someInternalFun() internal{}
  function somePrivateFun() private{}
}

contract AccessBaseContract is A{
  function call(){

    //访问父类的`public`方法
    somePublicFun();

    //访问父类的状态变量
    stateVar = 10;

    //访问父类的`internal`方法
    someInternalFun();

    //不能访问`private`
    //somePrivateFun();
  }
}
```
从上面的例子中，我们可以看到，子类可以访问父类的public，internal权限控制变量或函数，不能访问private权限控制的变量和函数。在子类中可以直接访问状态变量，原因是因为状态变量默认是internal的。

> 0.4.22颁布起，废除function+合约名称来定义构造函数
```
pragma solidity ^0.4.22;

contract DemoTest{
     constructor() public{
    }
}
```
### 继承支持传递参数
继承时可以有两种方式传参数到父类。下面来看第一种方式：
```
pragma solidity ^0.5.0;

contract Base{
  uint a;
  constructor(uint _a) public {
    a = _a;
  }
}

contract InheritPara is Base(1){
  function getBasePara() public view returns(uint){
    return a;
  }
}
```
另外一种方式是类似修改器的语法，来直接看一个例子：
```
pragma solidity ^0.5.0;

contract Base{
  uint a;
   constructor(uint _a) public {
    a = _a;
  }
}
contract InheritParaModifier is Base{
  constructor(uint _a) public Base(_a * _a){}
    
  function getBasePara() public view returns (uint){
    return a;
  }
}
```
如果要传入到基类的是简单的常量，第一种方式会更加简洁。但如果传入的参数与子类的输入参数有关，那么你应该使用第二种方式，以获取参数值。如果你同时使用了这两种方式，后一种方式将最终生效。

### 继承中的重名
继承中不允许出现相同的函数名，事件名，修改器名，或互相重名。
```
pragma solidity ^0.4.0;

contract Base1{
  address owner;
  modifier ownd(){
    if(msg.sender == owner) _;
  }

  event dupEvent(address, bytes);

  function dupFunc(){
    dupEvent(msg.sender, msg.data);
  }
}

contract Base2{
  address owner;
  modifier ownd(){
    if(msg.sender == owner) _;
  }

  event dupEvent(address, bytes);

  function dupFunc(){
    dupEvent(msg.sender, msg.data);
  }
}
//失败，将会报错 Identifier already declared
//contract DuplicateNames is Base1, Base2{}
  contract DuplicateNames is Base1{}
```
由于Base1，Base2拥有同样的修改器，方法，事件。如果同时继承将会报错。

还有一种比较隐蔽的情况，默认状态变量的getter函数导致的重名，下面来看一个例子：
```
pragma solidity ^0.4.0;

contract Base1{
  uint public data = 10;
}

contract Base2{
  function data() returns(uint){
    return 1;
  }
}

//一种隐蔽的情况，默认getter与函数名重名了也不行
//contract GetterOverride is Base1, Base2{}
contract GetterOverride is Base1{}
```
上面的例子中，Base1中的data由于生成了默认的getter函数data()，继承中也需要注意不能重名。
### 重写函数
在子类中允许重写函数，但不允许重写返回参数签名，一起来看看下面的代码：
```
pragma solidity ^0.4.0;

contract Base{
  function data() returns(uint){
    return 1;
  }
}

contract InheritOverride is Base{
  function data(uint){}
  function data() returns(uint){}
  //Override changes extended function signature
  //function data() returns(string){}
}
```
上面代码中的function data() returns(string){}将导致Override changes extended function signature报错，因为不能修改返回签名。

由于继承的实现方案是代码拷贝，所以合约继承后，部署到网络时，将变成一个合约。代码将从父类拷贝到子类中。

在继承链中，由于继承实现是代码复制。如果出现函数名重写，最终使用的是继承链上哪个合约定义的代码呢？实际执行时，依据的是最远继承的原则（most derived）。下面来看一个例子：
```
pragma solidity ^0.4.0;

contract Base1{
  function data() returns(uint){
    return 1;
  }
}

contract Base2{
  function data() returns(uint){
    return 2;
  }
}

contract MostDerived1 is Base1, Base2{
  function call() returns(uint){
    return data();
  }
}
contract MostDerived2 is Base2, Base1{
  function call() returns(uint){
    return data();
  }
}
```
上面的例子中，根据最远继承原则，大家可以想想MostDerived1，和MostDerived2中调用call()方法的运行结果。

实际上呢，MostDerived1，MostDerived2的call()将分别返回2和1。因为对于MostDerived1的最远继承合约是Base2，所以会使用其对应的函数，对于MostDerived2最远的继承者将是Base1。

### 继承父合约方法
当一个合约从多个其它合约那里继承，在区块链上仅会创建一个合约，在父合约里的代码会复制来形成继承合约。当一个合约继承了多个合约，因为存在最远继承原则，当一个合约调用一个重写方法时只会调用最远的的合约方法。

```
pragma solidity ^0.5.0;

contract mortal {
    function number() public returns(uint8) {
        return 1;
    }
}

contract Base1 is mortal {
    function number() public returns(uint8) {
        return 2;
    }
}


contract Base2 is mortal {
    function number() public returns(uint8) {
        return 3;
    }
}

contract Final is mortal, Base1 {
     function number() public returns(uint8) {
         return mortal.number();
     }
}
```
当部署合约`Final`调用`number`方法时，返回的数值是1，并不会返回Base1的2，但是当我们有一个清理作用的函数，想依次调用最远路径上的同名方法来帮我们清理一些数据，这个时候就需要用到一个关键字`super`。
```
contract Final is mortal, Base1 {
     function number() public returns(uint8) {
         return super.number();
     }
}
```
最终的调用顺序也会变成Final，Base2，Base1，motal。

### 多继承与线性化
在Solidity中，允许多继承，你可以同时继承多个合约。实现多继承的编程语言需要解决几个问题，其中之一是菱形继承问题又称钻石问题，如下图。
![](https://github.com/Ice-Storm/structure-and-interpretation-of-blockchain/blob/master/img/chapter_4/4_3.png?raw=true)

Solidity的解决方案参考Python，使用C3_linearization来强制将基合约转换一个有向无环图(DAG)的特定顺序。结果是我们希望的单调性，但却禁止了某些继承行为。特别是基合约在is后的顺序非常重要。下面的代码，Solidity会报错Linearization of inheritance graph impossible。
```
pragma solidity ^0.4.0;

contract X {}
contract A is X {}
contract C is A, X {}
```
原因是C会请求X来重写A(因为继承定义的顺序是A,X)，但A自身又是重写X的，所以这是一个不可解决的矛盾。

一个简单解决这种矛盾的原则是，总是指定基合约的继承顺序是从most base-like到most derived。
